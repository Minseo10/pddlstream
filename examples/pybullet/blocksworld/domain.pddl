(define (domain pr2-tamp)
  (:requirements :strips :equality)
  (:constants @sink @stove)
  (:predicates
    ; add for blocksworld
    (clear ?o)
    (stacked-on ?o ?undero)
    (on-table ?x)

    (table-support ?o ?p ?table)
    (block-support ?o ?p ?undero ?punder)
    (block ?o)
    (table ?table)

    (Arm ?a)
    (Stackable ?o ?r)
    (Sink ?r)
    (Stove ?r)
    (Type ?t ?b)
    (block ?o)
    (table ?table)

    (Pose ?o ?p)
    (Grasp ?o ?g)
    (Kin ?a ?o ?p ?g ?q ?t)
    (BaseMotion ?q1 ?t ?q2)
    (ArmMotion ?a ?q1 ?t ?q2)
    (Supported ?o ?p ?r)
    (BTraj ?t)
    (ATraj ?t)

    (CFreePosePose ?o ?p ?o2 ?p2)
    (CFreeApproachPose ?o ?p ?g ?o2 ?p2)
    (CFreeTrajPose ?t ?o2 ?p2)
    (CFreeTrajGraspPose ?t ?a ?o1 ?g1 ?o2 ?p2)

    (AtPose ?o ?p)
    (AtGrasp ?a ?o ?g)
    (HandEmpty ?a)
    (AtBConf ?q)
    (AtAConf ?a ?q)
    (CanMove)
    (Cleaned ?o)
    (Cooked ?o)

    (On ?o ?r)
    (Holding ?a ?o)
    (UnsafePose ?o ?p)
    (UnsafeApproach ?o ?p ?g)
    (UnsafeATraj ?t)
    (UnsafeBTraj ?t)
  )
  (:functions
    (Distance ?q1 ?q2)
    (MoveCost ?t)
    (PickCost)
    (PlaceCost)
  )

  (:action move_base
    :parameters (?q1 ?q2 ?t)
    :precondition (and (BaseMotion ?q1 ?t ?q2)
                       (AtBConf ?q1) (CanMove)
                       ;(not (UnsafeBTraj ?t))
                  )
    :effect (and (AtBConf ?q2)
                 (not (AtBConf ?q1)) (not (CanMove))
                 (increase (total-cost) (Distance ?q1 ?q2)))
                 ; (increase (total-cost) (MoveCost ?t)))
  )
  ;(:action move_arm
  ;  :parameters (?q1 ?q2 ?t)
  ;  :precondition (and (ArmMotion ?a ?q1 ?t ?q2)
  ;                     (AtAConf ?a ?q1))
  ;  :effect (and (AtAConf ?a ?q2)
  ;               (not (AtAConf ?a ?q1)))
  ;)

  (:action pick
    :parameters (?a ?o ?table ?p ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtPose ?o ?p) (HandEmpty ?a) (AtBConf ?q)
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                       ; added
                       (clear ?o)
                       (on-table ?o)
                       (table ?table)
                       (block ?o)
                  )
    :effect (and (AtGrasp ?a ?o ?g) (CanMove)
                 (not (AtPose ?o ?p)) (not (HandEmpty ?a))
                 (increase (total-cost) (PickCost))
                 ; added
                 (not (clear ?o)) (not (on-table ?o))
            )
  )
  (:action place
    :parameters (?a ?o ?table ?p ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtGrasp ?a ?o ?g) (AtBConf ?q)
                       (not (UnsafePose ?o ?p))
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                       ; added
                       (Holding ?a ?o)
                       (table ?table)
                       (block ?o)
                       (table-support ?o ?p ?table)
                  )
    :effect (and (AtPose ?o ?p) (HandEmpty ?a) (CanMove)
                 (not (AtGrasp ?a ?o ?g))
                 (increase (total-cost) (PlaceCost))
                 ; added
                 (clear ?o) (on-table ?o)
            )
  )
  (:action unstack
    :parameters (?a ?o ?undero ?p ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtPose ?o ?p) (HandEmpty ?a) (AtBConf ?q)
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                       ; added
                       (stacked-on ?o ?undero)
                       (clear ?o)
                       (block ?o) (block ?undero)
                       (not (= ?o ?undero))
                  )
    :effect (and (AtGrasp ?a ?o ?g) (CanMove)
                 (not (AtPose ?o ?p)) (not (HandEmpty ?a))
                 (increase (total-cost) (PickCost))
                 ; added
                 (clear ?undero)
                 (not (stacked-on ?o ?undero))
                 (not (clear ?o))
            )
  )
  (:action stack
    :parameters (?a ?o ?undero ?p ?punder ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtGrasp ?a ?o ?g) (AtBConf ?q)
                       (not (UnsafePose ?o ?p))
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                       ; added
                       (clear ?undero)
                       (Holding ?a ?o)
                       (block ?o) (block ?undero)
                       (not (= ?o ?undero))
                       (AtPose ?undero ?punder)
                       (block-support ?o ?p ?undero ?punder)
                  )
    :effect (and (AtPose ?o ?p) (HandEmpty ?a) (CanMove)
                 (not (AtGrasp ?a ?o ?g))
                 (increase (total-cost) (PlaceCost))
                 ; added
                 (clear ?o) (stacked-on ?o ?undero)
                 (not (clear ?undero))
            )
  )

  (:action clean
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Type ?r @sink) ; (Sink ?r)
                       (On ?o ?r))
    :effect (Cleaned ?o)
  )
  (:action cook
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Type ?r @stove) ; (Stove ?r)
                       (On ?o ?r) (Cleaned ?o))
    :effect (and (Cooked ?o)
                 (not (Cleaned ?o)))
  )

  (:derived (On ?o ?r)
    (exists (?p) (and (Supported ?o ?p ?r)
                      (AtPose ?o ?p)))
  )
  (:derived (Holding ?a ?o)
    (exists (?g) (and (Arm ?a) (Grasp ?o ?g)
                      (AtGrasp ?a ?o ?g)))
  )

  (:derived (UnsafePose ?o ?p)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreePosePose ?o ?p ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafeApproach ?o ?p ?g)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Grasp ?o ?g) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreeApproachPose ?o ?p ?g ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )

  (:derived (UnsafeATraj ?t)
    (exists (?o2 ?p2) (and (ATraj ?t) (Pose ?o2 ?p2)
                           (not (CFreeTrajPose ?t ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  ;(:derived (UnsafeBTraj ?t) (or
  ;  ;(exists (?o2 ?p2) (and (BTraj ?t) (Pose ?o2 ?p2)
  ;  ;                       (not (CFreeTrajPose ?t ?o2 ?p2))
  ;  ;                       (AtPose ?o2 ?p2)))
  ;  (exists (?a ?o1 ?g1 ?o2 ?p2) (and (BTraj ?t) (Arm ?a) (Grasp ?o1 ?g1) (Pose ?o2 ?p2)
  ;                                    (not (CFreeTrajGraspPose ?t ?a ?o1 ?g1 ?o2 ?p2)) (not (= ?o1 ?o2))
  ;                                    (AtGrasp ?a ?o1 ?g1) (AtPose ?o2 ?p2)))
  ;))

)